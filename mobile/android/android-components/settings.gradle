/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

pluginManagement {
    if (!gradle.root.hasProperty("mozconfig")){
        apply from: file('../gradle/mozconfig.gradle')
    } else {
        gradle.ext.mozconfig = gradle.root.mozconfig
        gradle.ext.configureMavenRepositories = gradle.root.ext.configureMavenRepositories
    }

    repositories {
        gradle.configureMavenRepositories(delegate)
    }

    includeBuild("../gradle/plugins/conventions")
    includeBuild("plugins/dependencies")
    includeBuild("plugins/publicsuffixlist")
    includeBuild("../gradle/plugins/nimbus-gradle-plugin")
}

plugins {
    id 'org.mozilla.conventions.settings'
    id 'mozac.DependenciesPlugin'
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            from(files("../../../gradle/libs.versions.toml"))
        }
    }
}

ext.topsrcdir = rootProject.projectDir.absolutePath.minus("mobile/android/android-components")

if (gradle.ext.mozconfig.substs.MOZ_APPSERVICES_IN_TREE) {
    apply from: file("${topsrcdir}/services/app-services/settings.gradle")
}

apply from: file('../shared-settings.gradle')
apply from: file('../autopublish-settings.gradle')

buildCache {
    local {
        directory = new File(rootDir, '.build-cache')
    }
}

def projectLocalProperties = file("local.properties").with { localPropertiesFile ->
    def localProperties = new Properties()
    if (localPropertiesFile.canRead()) {
        localPropertiesFile.withInputStream { localProperties.load(it) }
    }
    localProperties
}
projectLocalProperties.each { prop ->
    gradle.ext."localProperties.${prop.key}" = prop.value
}
